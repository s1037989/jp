#!/usr/bin/env perl

package Mojo::Collection::Role::Jp;
use Mojo::Base -strict, -role, -signatures;

use Mojo::Path;
use Scalar::Util qw(blessed);

sub csv ($self, $show_pointer, $delimiter="\t") {
  $self
    ->map(sub { blessed $_ && $_->isa('Jp') ? $_->csv($show_pointer, $delimiter) : $_})
    ->grep(sub{length $_})
    ->join("\n")
}

sub grep_defined ($self, $grep_defined) { $self->grep(sub { $grep_defined ? defined : 1 }) }

sub mjp ($self, @ARGV) { $self->map(sub { blessed $_ && $_->isa('Jp') ? $_->mjp(@ARGV) : $_ }) }

sub sort_pointer ($self, $sort) {
  return $self unless $sort;
  $self->sort(sub {
    my $_a = Mojo::Path->new($a->pointer);
    my $_b = Mojo::Path->new($b->pointer);
    if ($_a->[-1] =~ /^\d+$/ && $_b->[-1] =~ /^\d+$/) {
      $_a->[-1] <=> $_b->[-1]
    }
    else {
      $_a cmp $_b
    }
  })
}

package Jp;
use Mojo::Base -base, -signatures;
use overload '""' => sub { shift->to_string }, fallback => 1;

use Data::Walk::More;
use Mojo::Collection;
use Mojo::JSON qw(j);
use Mojo::JSON::Pointer;
use Mojo::Util;

has [qw(pointer value)];
has deref_arrays => 1;
has ignore_case => 0;
has remove_undef => 0;

Mojo::Util::monkey_patch __PACKAGE__,
  c => sub { Mojo::Collection->with_roles('+Jp')->new(@_) };

sub csv ($self, $show_pointer, $delimiter="\t") {
  c(($show_pointer?$self->pointer:()), grep { $self->remove_undef ? defined : 1 } $self->to_string($delimiter))->join($delimiter)
}

sub mjp ($self, @pointers) {
  my $P = Mojo::JSON::Pointer->new($self->value);
  @pointers = ('/') unless scalar @pointers;
  $self->new(
    deref_arrays => $self->deref_arrays,
    ignore_case => $self->ignore_case,
    pointer => $self->pointer,
    value => c(map {
      $self->new(
        deref_arrays => $self->deref_arrays,
        ignore_case => $self->ignore_case,
        pointer => $_,
        remove_undef => $self->remove_undef,
        value => $P->get(m!^/$!?'':$_)
      )
    } @pointers)
  );
}

sub json ($self) {
  my $value = $self->deref_arrays && ref $self->value eq 'ARRAY'
    ? @{$self->value}
    : ref $self->value ? j($self->value) : $self->value;
  return $value;
}

sub jp ($self, @pointer) {
  $self->{W} = {} unless defined $self->{W};
  walk(sub {
    my $p = join "/", '', @Data::Walk::More::indexes;
    $self->{W}->{($self->ignore_case?lc($p):$p)||'/'} = $_
  }, $self->value) unless scalar keys %{$self->{W}};
  foreach my $pointer (@pointer) {
    my $value = $self->{W}->{$self->ignore_case ? lc($pointer) : $pointer};
    return $value if $value;
  }
}

sub to_string ($self, $delimiter="\t") {
  return ref $self->value && $self->value->isa('Mojo::Collection')
    ? $self->value->map('json')->grep_defined(!$self->remove_undef)->join($delimiter)
    : $self->json;
  my $value = $self->value;
  if (!ref $value) { return $value }
  elsif (ref $value eq 'HASH') { return $self->json }
  elsif (ref $value eq 'ARRAY') { return $self->json }
  elsif ($value->isa('Mojo::Collection')) { return $value->map('json')->grep_defined(!$self->remove_undef)->join($delimiter) }
  else { return $self->json }
}

package main;
use Mojo::Base -strict, -signatures;

use Data::Walk::More;
use Mojo::ByteStream;
use Mojo::Collection;
use Mojo::File;
use Mojo::JSON qw(j);
use Mojo::JSON::Pointer;
use Mojo::URL;
use Mojo::Util qw(dumper);
use Scalar::Util qw(blessed);

use constant DEBUG => $ENV{JP_DEBUG} || 0;
use constant TRAVERSE => !!(eval { require Data::Traverse; Data::Traverse::ignore_unsupported_refs(1); 1 });
Data::Traverse->import('traverse') if TRAVERSE;

Mojo::Util::monkey_patch __PACKAGE__,
  c => sub { Mojo::Collection->with_roles('+Jp')->new(@_) },
  f => sub { Mojo::File::path(@_) },
  l => sub { Mojo::URL->new(@_) },
  r => sub { Mojo::Util::dumper(@_) };

my $DEBUG;
my $OUT;
my $stdin;

Mojo::Util::getopt \@ARGV, ['bundling'],
  'A'   => \my $deref_arrays,
  'C'   => \my $no_collection,
  'E=s' => \my $eval,
  'I=s' => \my $is_test,
  'M=s' => \my $message,
  'R=s' => \my $regex_test,
  'S'   => \my $dont_sort,
  'T=s' => \my $eval_test,
  'U'   => \my $remove_undef,
  'a=s' => \my @ARGS,
  'd=s' => \(my $delimiter = "\t"),
  'h+'  => \my $help,
  'i'   => \my $ignore_case,
  'k=i' => \my $tail,
  'n=i' => \my $head,
  'p'   => \my $show_pointer,
  'r=s' => \(my $regex = '/'),
  'v'   => \my $verbose;

&_usage($help) if $help || ((scalar grep {defined $_} ($is_test, $regex_test, $eval_test)) > 1 ? 1 : 0);

local $_ = &_walk(&_ptr(&_stdin))->grep(sub {
  $_->pointer =~ ($ignore_case ? qr(^$regex$)i : qr(^$regex$)) or return 0 if $regex;
  return 1;
});

$eval = "no warnings; $eval" if !$verbose && $eval;
@_ = $eval ? eval $eval : ($_);
chomp(my $eval_err = $@ =~ s/at \(eval \d+\) line \d+.*/in -E eval/r);
warn $eval_err if $verbose && $@;
$_ = $#_ == 0 && blessed $_[0] && $_[0]->isa('Mojo::Collection') ? $_[0] : c(@_);
$_ = $_->sort_pointer(!$dont_sort)->mjp(@ARGV);

_say(
  defined $head || defined $tail
    ?
      join "\n",
        (defined $head ? $_->head($head)->csv($show_pointer, $delimiter) : ''),
        (defined $tail ? $_->tail($tail)->csv($show_pointer, $delimiter) : '')
    :
      $_->csv($show_pointer, $delimiter)
);

sub D { $DEBUG=1; _debug(@_); $DEBUG=0; return (@_); }

sub jp (@pointer) {
  my $W;
  walk(sub {
    my $p = join "/", '', @Data::Walk::More::indexes;
    $W->{($ignore_case?lc($p):$p)||'/'} = $_
  }, $_->value);
  foreach my $pointer (@pointer) {
    my $value = $W->{$ignore_case ? lc($pointer) : $pointer};
    return $value if $value;
  }
}

sub out { $OUT.=shift."\n" }

sub _debug { warn dumper({ref($_[0]) => [@_?@_:$_]}) if DEBUG || $DEBUG }

sub _ptr { Mojo::JSON::Pointer->new(j(shift//$stdin)) }

sub _stdin {
  local $/ = undef;
  $stdin ||= <STDIN>;
  return $stdin;
}

sub _say ($say) {
  my $test = $is_test // $regex_test // $eval_test;
  $OUT && not defined $test and chomp $OUT unless length $say;
  $say = $OUT.$say if $OUT;
  chomp $say;
  do { say $say if length $say; exit } if not defined $test;
  $message = join ' ', ($message//'Test'), @ARGV if $message || not defined $message;
  $say ||= $eval_err if $eval_err;
  local $_ = $say;
  my $exit;
  if (defined $is_test) {
    $exit = $test eq $_ ? 0 : 1;
  }
  elsif (defined $regex_test) {
    $exit = $_ =~ qr(^$test$) ? 0 : 1;
    $test = "qr(^$test\$)";
  }
  elsif (defined $eval_test) {
    my $j = j($_);
    $exit = eval $test ? 0 : 1;
  }
  $say =~ s/\n/\\n/g; $say =~ s/\t/\\t/g;
  $test =~ s/\n/\\n/g; $test =~ s/\t/\\t/g;
  say $exit ? "\033[31mfail\033[0m: '$message' is '$say', expected '$test'" : "\033[32mok\033[0m: '$message' is '$test'" if $message;
  exit $exit;
}

sub _usage ($help) {
  print <<EOF;
  Usage: $0 [OPTIONS] [POINTERS]

    Options:
      -A               Dereference JSON arrays into columns
      -C               No collection
      -E eval-string   Evaluate the Perl eval string
                       These functions and variables, in addition to standard Perl functions and variables, are:
         \$_            A Mojo::Collection of [JP OBJECTS]
         D             Dump an object to inspect it
         out           Print to stdout and include it in test inspection
         c             Create a new Mojo::Collection object
         f             Create a new Mojo::File object
         l             Create a new Mojo::URL object
         r             Alias to Mojo::Util::dumper
         traverse      Imported from Data::Traverse, if available
      -I exact-match   Compare the results exactly against the supplied text
      -M test-message  Specify a message in the test output
      -R regex         Compare the results against the supplied regex
      -S               Don't sort
      -T eval-string   Compare the results against the supplied Perl eval string
      -U               Remove undef
      -d delimiter     Delimiter to use in columnar output
      -h               Display this help, one more to include examples
      -i               Ignore case in regex JSON pointers
      -k tail-number   Display the bottom n results, n can be negative
      -n head-number   Display the top n results, n can be negative
      -p               Include the pointer in the columnar output
      -r regex-pointer A regex JSON pointer for selecting multiple targets
      -v               Increase verbosity
    
    Jp Objects:
      An object with pointer and value attributes. The primary method to use for this object when iterating a collection
      is the `jp` method which is used to further expand the JSON object by pointer. See the example commented
      "Filtering" for a very practical use case of the Jp object which allows further filtering the results of the regex
      pointer (-r) by iterating the \$_ Mojo::Collection of Jp objects with the use of the `grep` method and using a
      JSON Pointer with the `jp` method on each Jp object.

    Pointers:
      Any remaining non-flag arguments are considered JSON pointers and used to narrow the selection from the selected
      JSON string for columnar output.
EOF
  exit 1 unless $help > 1;
  print <<'EOF';

  Examples:
    (All examples read example.json from stdin, as shown by the first example)

    # Use a JSON pointer to get a value from the JSON data structure
    $ jp /artifactory/0/password < example.json
    s3cr3t

    # Use a JSON pointer to get a value from the JSON data structure, defaults to /
    $ jp
    {"Version":"1","artifactory":[{...},{...},{...}]}

    # Use a regex in the pointer to reduce the JSON data structure and return each result, one per line
    $ jp -n 1 -r '/artifactory/\d+'
    {...}

    # Use a regex in the pointer to reduce the JSON data structure and include the pointer for all records found
    $ jp -n 1 -p -r '/artifactory/\d+' /user /password
    /artifactory/0  12345 s3cr3t

    # Filtering
    $ jp -Aip -n -1 -d: -E '$_->grep(sub{$_->jp("/isdefault")})' -r '/Artifactory/\d' /user /password
    /artifactory/1:54321:s3cr3t5

    # Treat the execution as a test
    $ jp -n 1 -E '$_->tap(sub{out $_->size})->tap(sub{out $_->size})' -r '/artifactory/\d+' /user /password \
        -T '3\n3\n12345\ts3cr3t'
    ok: 'Test /user /password' is '3\n3\n12345\ts3cr3t'

    # A syntax error in the -E Perl eval is handled gracefully (final tap method is missing a closing ')')
    $ jp -v -n 1 -E '$_->tap(sub{out $_->size})->tap(sub{die 123}' -r '/artifactory/\d+' /user /password 
    syntax error in -E eval

    # Don't sort by pointer, sort arbitrarily as specified in the -E Perl eval
    $ jp -v -S -U -p -r '/markers/\d' -E '$_->sort(sub{$a->jp("/location/0", "/position/0") <=> $b->jp("/location/0", "/position/0")})' /location /position <<EOF
    {"markers":[{"name":"Google, 8th Avenue","position":[40.7414728,-74.0055813]},{"name":"Microsoft, Times Square","location":[40.7565323,-73.9904037]},{"name":"Tesla, Washington Street","location":[40.7411595,-74.0097167]},{"name":"Amazon New York","location":[40.7532822,-74.0010696]}]}
    EOF
EOF
  exit 1;
}

sub _walk ($P, $pointer='/') {
  my $W;
  local $_ = $pointer;
  walk(sub {
    my $p = join "/", '', @Data::Walk::More::indexes;
    $W->{($ignore_case?lc($p):$p)||'/'} = $_
  }, $P->get(m!^/$!?'':$_));
  c(map {
    Jp->new(
      deref_arrays => $deref_arrays,
      ignore_case => $ignore_case,
      pointer => $_,
      remove_undef => $remove_undef,
      value => $W->{$_}
    )
  } keys %$W)
}
